//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
// Грааль_2.mq4.
// Используется в качестве примера в статье "Мой первый Грааль".
// Сергей Ковалёв, Днепропетровск, sk@mail.dnepr.net, ICQ 64015987, http://autograf.dp.ua/.
//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
extern int TakeProfit=5;                                 // TakeProfit ордера
extern int StopLoss= 29;                                 // StopLoss ордера
extern int Distan   = 2;                                 // Дистанция от средней МА
extern int Cls      = 2;                                 // Закрыть при ** пнктов прибыли
extern int period_MA=16;                                 // Период средней МА 
//extern int Time_1   = 0;                               // Время начала работы 
//extern int Time_2   = 0;                               // Время окончания работы
extern int Prots    = 0;                                 // Процент от свободных средств

//--------------------------------------------------------------------------------------------
int
   Nom_bl,                                               // Номер ордера BuyLimit
   Nom_sl,                                               // Номер ордера SellLimit
   total,                                                // Количество лотов
   bl = 0,                                               // 1 = факт налиия ордера BuyLimit
   sl = 0,                                               // 1 = факт налиия ордера SellLimit
   b  = 0,                                               // 1 = факт налиия ордера Buy
   s  = 0;                                               // 1 = факт налиия ордера Sell 
//--------------------------------------------------------------------------------------------
double 
   OP,                                                   // OpenPrice (абсолютн. пунктов)
   SL,                                                   // StopLoss   ордера (относит.пункт.)
   TP,                                                   // TakeProfit ордера (относит.пункт.)
   dist,                                                 // Дистанция от средней МА(отн.пун.)
   Level,                                                // Миним. допуст дистанция отл.орд
   OP_bl,                                                // OpenPrice BuyLimit (абс. пунктов)
   OP_sl,                                                // OpenPrice SellLimit(абс. пунктов)
   cls,                                                  // Закрыть при ** прибыли (абс. пун.)
   MA,                                                   // Значение МА (курс)
   spred,                                                // Спред (абс. пунктов)
   Lot;                                                  // Количество лотов
//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
int init()
   {   
   Level=MarketInfo(Symbol(),MODE_STOPLEVEL);            // Выясним что нам даёт сервер
   Level=(Level+1)*Point;                                // ?:)
   SL=StopLoss*Point;                                    // StopLoss   ордера (относит.пункт.)
   TP=TakeProfit*Point;                                  // TakeProfit ордера (относит.пункт.)
   dist=Distan*Point;                                    // Дистанция от средней МА(отн.пун.)
   cls=Cls*Point;                                        // Закрыть при ** прибыли (абс. пун.)
   spred=Ask-Bid;                                        // Спред (абс. пунктов)
   return;
   } 
//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
int start()
   {   
//============================================================================================
   total=OrdersTotal();                                  // Количество лотов
   bl=0;                                                 // Обнулимся в начале прохода
   sl=0;                                                 // Обнулимся в начале прохода
   b=0;                                                  // Обнулимся в начале прохода
   s=0;                                                  // Обнулимся в начале прохода
//--------------------------------------------------------------------------------------------
   for (int i=total; i>=0; i--)                          // По всем ордерам
      {                                               
      if (OrderSelect(i,SELECT_BY_POS)==true &&         // Выделим ордер
         OrderSymbol()==Symbol())
         {
      
//--------------------------------------------------------------------------------------------
         if (OrderType()==OP_BUY)                        // Ордер Buy
            {
            b =1;                                        // Есть такой ордер
            Close_B(OrderTicket(),OrderLots());          // Закрыть ордер (надо ли решит ф-ия)
            }
//--------------------------------------------------------------------------------------------
         if (OrderType()==OP_SELL)                        // Ордер Sell
            {
            s =1;                                        // Есть такой ордер
            Close_S(OrderTicket(),OrderLots());          // Закрыть ордер (если надо)
            }
//--------------------------------------------------------------------------------------------
         if (OrderType()==OP_BUYLIMIT)                   // Ордер BuyLimit
            {
            OP_bl=NormalizeDouble(OrderOpenPrice(),Digits);//OpenPrice BuyLimit(абс. пунктов)
            Nom_bl=OrderTicket();
            bl=1;                                        // Есть такой ордер
            }
//--------------------------------------------------------------------------------------------
         if (OrderType()==OP_SELLLIMIT)                  // ОрдерSellLimit
            {
            OP_sl=NormalizeDouble(OrderOpenPrice(),Digits);//OpenPrice SellLimit(абс.пунктов)
            Nom_sl=OrderTicket();
            sl=1;                                        // Есть такой ордер
            }
//--------------------------------------------------------------------------------------------
         }
      }   
//--------------------------------------------------------------------------------------------
   MA = iMA(NULL,0, period_MA, 0,MODE_LWMA, PRICE_TYPICAL, 0);// Текущее значение МА
   Modify_order();                                       // Активизируем модификацию
   Open_order() ;                                        // Активизируем открытие
//============================================================================================
   return;
   } 
//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
void Close_B(int Nomber, double lots)                    // Закрытие ордеров Buy
   {
//============================================================================================
   if (NormalizeDouble(Bid-OrderOpenPrice(),Digits)>=cls)// Если достигнут заданный профит
      {
      OrderClose( Nomber, lots, Bid, 1, Yellow);         // Закрываемся
      b = 0;                                             // И больше нет бая
      }
//============================================================================================
   return;
   }
//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
void Close_S(int Nomber, double lots)                    // Закрытие ордеров Sell
   {
//============================================================================================
   if (NormalizeDouble(OrderOpenPrice()-Ask,Digits)>=cls)// Если достигнут заданный профит
      {
      OrderClose( Nomber, lots, Ask, 1, Yellow);         // Закрываемся
      s = 0;                                             // И больше нет селла
      }
//============================================================================================
   return;
   }
//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
void Modify_order()                                      // Модификация ордеров
   {
//============================================================================================
   if (bl==1)                                            // Если есть БайЛимит
      {
      OP=MA-dist;                                        // Он должен стоять здесь
      if (MathAbs(OP_bl-OP)>0.5*Point)                   // А если он здесь не стоит
         {
         OrderModify(Nom_bl,OP,OP-SL,OP+TP,0,DeepSkyBlue);// Модификация ордера 
         }
      }
//--------------------------------------------------------------------------------------------
   if (sl==1)                                            // Если есть СеллЛимит
      {
      OP=MA+spred+dist;                                  // Он должен стоять здесь
      if (MathAbs(OP_sl-OP)>0.5*Point)                   // А если он здесь не стоит
         {
         OrderModify( Nom_sl, OP, OP+SL, OP-TP, 0, Pink);// Модификация ордера 
         }
      }
//============================================================================================
   return;
   }
//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
void Open_order()                                        // Открывающая функция
   {
//   int Tek_Time=TimeHour(CurTime());                   // Для тестирования по времени
//   if (Tek_Time>Time_2 && Tek_Time<Time_1) return;     // Для тестирования по времени
//============================================================================================
   if (b==0 && bl==0)                                    // Нет никаких баёв, открываем bl
      {
      OP=MA-dist;                                        // Курс открытия ордера bl      
      if(OP>Ask-Level) OP=Ask-Level;                     // Уточнение ОР в соотв. с допуском
      OP=NormalizeDouble(OP,Digits);                     // Нормализация (МА даёт 5й знак) 
      OrderSend(Symbol(),OP_BUYLIMIT, Lots(),OP,3,OP-SL,OP+TP,"",0,0,Blue);// Открываемся
      bl=1;                                              // Теперь есть бай
      }      
//--------------------------------------------------------------------------------------------
   if (s==0 && sl==0)                                    // Нет никаких селов, открываем sl
      {
      OP=MA+spred+dist;                                  // Курс открытия ордера sl     
      if(OP<Bid+Level) OP=Bid+Level;                     // Уточнение ОР в соотв. с допуском
      OP=NormalizeDouble(OP,Digits);                     // Нормализация (МА даёт 5й знак) 
      OrderSend(Symbol(),OP_SELLLIMIT,Lots(),OP,3,OP+SL,OP-TP,"",0,0,Red);// Открываемся
      sl=1;                                              // Теперь есть sl
      }
///============================================================================================
   return;
   }
//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
double Lots()                                            // Вычисление лотов
   {
//============================================================================================
   Lot=NormalizeDouble(AccountEquity()*Prots/100/1000,1);// Вычисляем колич. лотов  
   double Min_Lot = MarketInfo(Symbol(), MODE_MINLOT);   // Минимально допустимая стоим. лотов
   if (Lot == 0 ) Lot = Min_Lot;                         // Для теста на постоян. миним. лотах
//============================================================================================
   return(Lot);
   }
//жжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжжж
